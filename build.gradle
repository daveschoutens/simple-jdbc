plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'io.github.daveschoutens.simple-jdbc'
version = '0.9.2'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'com.google.guava:guava:31.0.1-jre'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'com.zaxxer:HikariCP:4.0.3' // Cannot upgrade to 5.0.0 due to JDK version
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.16.2'
    testImplementation 'org.testcontainers:postgresql:1.16.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.postgresql:postgresql:42.3.1'
}

compileJava {
    sourceCompatibility = 1.8
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

signing {
//    useGpgCmd()
    sign publishing.publications.maven
}

project.plugins.withType(MavenPublishPlugin).all {
    PublishingExtension publishing = project.extensions.getByType(PublishingExtension)
    publishing.publications.withType(MavenPublication).all { mavenPublication ->
        mavenPublication.pom {
            name = "${project.group}:${project.name}"
            description = name
            url = 'https://github.com/daveschoutens/simple-jdbc'
            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id = 'daveschoutens'
                    name = 'Dave Schoutens'
                    email = 'dschoutens@gmail.com'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/daveschoutens/simple-jdbc'
                developerConnection = 'scm:git:ssh://github.com/daveschoutens/simple-jdbc.git'
                url = 'https://github.com/daveschoutens/simple-jdbc'
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'local'
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
